#!/usr/bin/env bpftrace
/*
 * fault_vs_khugepaged.bt
 *
 * For a target PID ($1), prints the number of page faults between
 * successive khugepaged scan events. One line per scan:
 *     scan <fault_count>
 *
 * This preserves event ordering â€” the sequence of numbers shows how
 * many faults accumulated between each consecutive pair of scans.
 * Scalar counters and scan status are dumped at END (SIGINT).
 *
 * Usage: sudo ./fault_vs_khugepaged.bt <target_pid>
 * Terminate with SIGINT (Ctrl-C or pkill -2).
 *
 * Tracepoints (stable ABI, verified on 6.13.8 and 6.16.7):
 *   exceptions:page_fault_user       - address, ip, error_code
 *   huge_memory:mm_khugepaged_scan_pmd - mm, pfn, writable, referenced,
 *                                        none_or_zero, status, unmapped
 */

tracepoint:exceptions:page_fault_user
/pid == $1/
{
	@_fc_scan++;
	@total_faults = count();
}

tracepoint:huge_memory:mm_khugepaged_scan_pmd
{
	printf("scan %lld\n", @_fc_scan);
	@_fc_scan = 0;

	@total_scans = count();
	@scan_status[args->status] = count();
}

END
{
	clear(@_fc_scan);
}
